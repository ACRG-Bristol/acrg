pro plotvertfield,datadir,selgrid,selspecies=selspecies,$
    selfield=selfield,sellevel=sellevel,zoom=zoom,filetext=filetext,$
    multiplot=multiplot,plotheader=plotheader,plotlegend=plotlegend,$
    genanim=genanim,usecontours=usecontours,group=group,$
    exact=exact,template=template,vgrid=vgrid,$
    plotloc=plotloc,type=type,gengif=gengif,genjpg=genjpg,$
    ncontours=ncontours,back=back,ct=ct,$
    percent=percent,last=last,xpixel=xpixel,contourstep=contourstep,$
    scalefactor=scalefactor,namever=namever,timeframe=timeframe,$
    titletext=titletext,SelTimeAverage=SelTimeAverage,$
    relduration=relduration,placenames=placenames,$
    selcase=selcase,seldatavalue=seldatavalue,selname=selname,$
    ct_blue2red=ct_blue2red,metoffice=metoffice
    
  ;--------------------------------------------------------------------
  ; Procedure to generate sequence of selected plots
  ;
  ;
  ; SJL Aug 2009
  ; (Created by modifying plotfield.pro)
  ;--------------------------------------------------------------------
  ;
  ; Arguments
  ; required:
  ;  datadir      : (string)  run directory
  ;  selgrid      : (string)  grid ('grid1' or 'grid2')
  ;
  ; optional:
  ;  selspecies   : (string or string array) species(s) to plot
  ;  selfield     : (string or string array) field(s) to plot
  ;  sellevel     : (string or string array) level(s) to plot
  ;
  ;  SelTimeAverage  : (string) time averaged field to plot
  ;
  ;  zoom         : fltarr(4) area to plot (eg zoom=[-20,30,35,65])
  ;  filetext     : (string)  text added to filenames
  ;  multiplot    : intarr(3) number of plots,
  ;                 (eg multiplot=[0,2,2] for 2 by 2 plots)
  ;                 first element can be any value
  ;  plotheader   : integer (0 or 1) =1 adds run information and title
  ;                 to plots
  ;  plotlegend   : integer (0 or 1) =1 plots legend below each plot
  ;  genanim      : integer (0 or 1) =1 generates gifs animation
  ;  usecontours  : fltarr(number of fields,number of species,*)
  ;                 array of contour values for each field
  ;                 if not passed levels determined automatically
  ;  group        : string ('field','time') determines what
  ;                 order to plot fields
  ;  exact        : integer (0 or 1) =1 map area based on lon/lat limits
  ;                  loosely based on RIMNET ranges
  ;  template     : string - string to include when listing Fields* files
  ;                 eg ='0000' to search for midnight data only
  ;  plotloc      : string filename conatining locations to overplot,
  ;                 see plotloc.pro for file format
  ;  placenames   : set to 0 to not plot names of locations in plotloc file
  ;  type         : string ('pixel' or 'contour') choose pixels or
  ;                  filled contours
  ;  gengif       : integer (0 or 1) =1 generates gif images (on unix)
  ;  genjpg       : integer (0 or 1) =1 generates jpg images (on unix)
  ;  ncontours    : integer - number of contours
  ;  back         : integer (0 or 1 or 2) =1 plots in reverse order
  ;                 =2 takes title from CTITLE
  ;                 =3 like 1 but not in reverse order
  ;  ct           : integer (1-16) pvwave colour table
  ;  percent      : integer (0 or 1) =1 plots back attribution
  ;                  as percentage contribution. Only applies if back ne 0
  ;  last         : integer (0 or 1) =1 plots last timestep only
  ;  xpixel       : integer - size of images generated by genanim
  ;  contourstep  : real - countour spacing (use le 1 for linear scaling)
  ;  scalefactor  : real - scaling factors to apply to data
  ;  namever      : integer indicating version of model e.g. 3 - NAME III(PPM)
  ;                 MUST be NAME III. Currently no intention to make
  ;                 NAME II compatible
  ;  timeframe    : string - 'absolute' or 'relative' time frame for NAME III
  ;  titletext    : string - text string added to title of run for output
  ;  relduration  : real - Duration of release in seconds (Back run only)
  ;                 Only required if Percentage Air Concentration asked for
  ;  selcase      : case number (name iii only - defaults to 1 if not given)
  ;  seldatavalue : data value (i.e. percentile or probability)
  ;                 (name iii only - defaults to ' ' if not given)
  ;  selname      : name of field requirement (can be used to specify
  ;                 a field which is not uniquely defined by the other
  ;                 parameters; name iii only - defaults to ' ' if not given)
  ;  ct_blue2red  : integer (0 or 1) =1 calls a custom colour table,
  ;                 which goes from blue to red, ideal for plotting
  ;                 temperature and humidity fields etc
  ;  metoffice    : integer (0 or 1) =1 (default) prints met office logo
  ;                 in PS file
  ;  vgrid        : user input values of nz, dz and z0 (as in Vertical
  ;                 grids in NAME input file). Cannot be used to
  ;                 restrict plot range
  ;
  ; Example of Use
  ;seldir='/home/h02/apsl/name/nwp_examples/jul02_regmet/output'
  ;selspecies=['TRACER']
  ;selfield=['Air Concentration']
  ;SelTimeAverage=['1hr 0min integral']
  ;selgrid='NS_grid1z'
  ;sellevel=['X = -3.136700 Lat-Long']
  ;vgrid=[70.0,10.0,5.0]
  ;plotvertfield,seldir,selgrid,/exact,sellevel=sellevel,$
  ; selspecies=selspecies,selfield=selfield,$
  ; group='time',gengif=0,vgrid=vgrid,$
  ; multiplot=[0,2,2],plotheader=1,plotlegend=1,genanim=0,$
  ; SelTimeAverage=SelTimeAverage,namever=3
  ;
    
  ; note:
  ; (1) selfield and sellevel must contain same number of requests,
  ;     otherwise all attribution fields plotted
  ;
  ; subroutine calls:
  ;  readfieldhead
  ;  readvertfield
  ;  genposition
  ;  plotvertpos
  ;  genanim
  ;  pixelplot
  ;
  ;-----------------------------------------------------------------------
    
    
  ;-----------------------------------------------------------------------
  ; arguments
    
  if(n_elements(back) eq 0)then back=0
  if(n_elements(type) eq 0)then type='pixel'
  if(n_elements(plotloc) eq 0)then plotloc=''
  if(n_elements(titletext) eq 0)then titletext=''
  if(n_elements(exact) eq 0)then exact=0
  if(n_elements(group) eq 0)then group='time'
  if(n_elements(plotlegend) eq 0)then plotlegend=1
  if(n_elements(plotheader) eq 0)then plotheader=1
  if(n_elements(multiplot) eq 0)then multiplot=[0,1,1]
  if(n_elements(genanim) eq 0)then genanim=1
  if(n_elements(gengif) eq 0) then gengif=0
  if(n_elements(genjpg) eq 0) then genjpg=0
  if(n_elements(ncontours) eq 0) then ncontours=11
  if(n_elements(filetext) eq 0)then begin
    filetext=''
  endif else begin
    filetext=filetext+'_'
  endelse
  if(n_elements(template) eq 0)then template='*'
  if(n_elements(ct) eq 0)then ct=5
  if(n_elements(percent) eq 0)then percent=0
  if(n_elements(last) eq 0)then last=0
  if(n_elements(xpixel) eq 0)then xpixel=595
  if(n_elements(contourstep) eq 0)then contourstep=3.16227766
  if(n_elements(scalefactor) eq 0)then scalefactor=1.0
  if(n_elements(usecontours) gt 0)then contours=usecontours
  if(n_elements(namever) eq 0)then namever=2
  if(n_elements(timeframe) eq 0)then timeframe='absolute'
  if(n_elements(relduration) eq 0)then relduration=0.
  if(n_elements(selcase) eq 0)then begin
    selcase=1
    printcasenum=0
  endif else begin
    printcasenum=1
  endelse
  if(n_elements(seldatavalue) eq 0)then seldatavalue=' '
  if(n_elements(ct_blue2red) eq 0)then ct_blue2red=0
  if(n_elements(metoffice) eq 0)then metoffice=1
  if(n_elements(placenames) eq 0) then placenames=1
  if(n_elements(vgrid) eq 0) then vgrid=[0.0,0.0,0.0]
  
  ;-----------------------------------------------------------------------
  ;-----------------------------------------------------------------------
  ; graphics initialisation
  
  if(strpos(!version.os,'NT') ge 0)then begin
    pc=1
    delim='\'
  endif else begin
    pc=0
    delim='/'
  endelse
  
  if(strpos(!version.os,'Linux') ge 0)then begin
    linux=1
  endif else begin
    linux=0
  endelse
  
  xysize=1.0
  axiscol=0
  !p.font=0
  !x.thick=1
  !y.thick=1
  
  nxplot=multiplot(1)
  nyplot=multiplot(2)
  
  if(plotheader ne 0)then begin
    position=[0.00,0.15,1.00,0.87]
  endif else begin
    position=[0.0,0.0,1.0,1.0]
  endelse
  
  ;if (rimnet ne 0)then ncontours=11
  
  if(back ne 0)then begin
    ct= 15
    print,'back option set - forcing loadct,15'
  endif
  
  if(back eq 2) then begin
    percent=1
    print,'Setting percent=1, assume cstart and end on CTITLE'
  endif
  
  ;-----------------------------------------------------------------------
  ; find all files and readheader
  
  if (namever eq 3)then begin
    ; NAME III
    casenum=STRTRIM(selcase,2)
    fieldfiles=findfile(datadir+delim+selgrid+'_C'+casenum+'_T*')
    
    fieldfiles1=fieldfiles
    numfiles=n_elements(fieldfiles)
    
    FileSplit = STRSPLIT(fieldfiles(0),'_')
    NFileSplit = n_elements(FileSplit)
    
    TSection=strarr(numfiles)
    
    ; split file names into array elements. '_' used as dividing points
    ; this enables us to sort and order files correctly.
    for i=0,numfiles-1 do begin
      FileSplit = STRSPLIT(fieldfiles(i),'_',/Extract)
      TSection(i) = FileSplit(NFileSplit-2)
    endfor
    
    ; search through TSection array to correctly order files.
    
    for i=1,numfiles do begin
      Tname=STRCOMPRESS('T'+string(i), /Remove_All)
      for j=0,numfiles-1 do begin
        if (TSection(j) eq Tname)then begin
          fieldfiles1(i-1) = fieldfiles(j)
        endif
      endfor
    endfor
    
    fieldfiles = fieldfiles1
    
  endif else begin
    ;NAME
    fieldfiles=findfile(datadir+delim+'Fields_'+selgrid+'_*'+template+'*')
  endelse
  
  if(back eq 1)then fieldfiles=reverse(fieldfiles)
  
  numfiles=n_elements(fieldfiles)
  
  if(pc eq 1)then begin
    for i=0,numfiles-1 do begin
      fieldfiles(i)=datadir+delim+fieldfiles(i)
    endfor
  endif
  
  readfieldhead,fieldfiles(0),modtitle,fieldhead,xyrel,$
    namever=namever,timeframe=timeframe
    
  ; all times
    
  timelist=strarr(numfiles)
  for it=0,numfiles-1 do begin
    if (namever eq 3)then begin
      ; NAME III
      if (it eq 0) then begin
        FileTime=strarr(2)
      endif
      FileSplit = STRSPLIT(fieldfiles(it),'_',/Extract)
      NFileSplit = n_elements(FileSplit)
      TimeBit = FileSplit(NFileSplit-1)
      FileTime = STRSPLIT(TimeBit,'.t',/Extract)
      timelist(it)=FileTime(0)
    endif else begin
      ; NAME
      ;    timelist(it)=strmid(fieldfiles(it),(strpos(fieldfiles(it),'Field'))+13,12)
      timelist(it)=strmid(fieldfiles(it),(strpos(fieldfiles(it),'.txt'))-12,12)
    endelse
  endfor
  
  if(last ne 0)then begin
    timelist=timelist(numfiles-1)
    fieldfiles=fieldfiles(numfiles-1)
    numfiles=1
  endif
  
  ; all species
  
  if (namever eq 3)then begin
    ; NAME III
    if(n_elements(selspecies) eq 0)then begin
      array=fieldhead(where(fieldhead(*,3)),3)
      specieslist=array[uniq(array,sort(array))]
      numspecies=n_elements(specieslist)
    endif else begin
      numspecies=n_elements(selspecies)
      specieslist=selspecies
    endelse
  endif else begin
    ;NAME
    if(n_elements(selspecies) eq 0)then begin
      array=fieldhead(where(fieldhead(*,1)),1)
      specieslist=array[uniq(array,sort(array))]
      numspecies=n_elements(specieslist)
    endif else begin
      numspecies=n_elements(selspecies)
      specieslist=selspecies
    endelse
  endelse
  
  ; all fields
  
  if n_elements(numfields) eq 0 then begin
    numfields=n_elements(where(fieldhead(*,0)))
  endif
  
  if (namever eq 3)then begin
    ; NAME III
  
    if ((n_elements(selfield) eq 0) OR (n_elements(sellevel) eq 0)) then begin
      fieldlist=reform(fieldhead(*,2))
      fieldlist=fieldlist(where(fieldlist))
      levellist=reform(fieldhead(*,15))
      levellist=levellist(where(levellist))
    endif else begin
      numfields=n_elements(selfield)
      fieldlist=selfield
      if (n_elements(sellevel) ne numfields) then begin
        print, 'ERROR - incorrect number of entries in level list'
        exit
      endif else begin
        levellist=sellevel
      endelse
    endelse
    
    if (n_elements(SelTimeAverage) eq 0) then begin
      timeaveraginglist=reform(fieldhead(*,7))
      timeaveraginglist=timeaveraginglist(where(timeaveraginglist))
    endif else begin
      timeaveraginglist=SelTimeAverage
    endelse
    
    if (n_elements(SelTimeAverage) ne numfields) then begin
      print,'ERROR - incorrect number of entries in time-averaging list'
      exit
    endif
    
    if (n_elements(selname) eq 0) then begin
      FieldNameList=replicate(' ',numfields)
    endif else if (n_elements(selname) ne numfields) then begin
      print,'ERROR - incorrect number of entries in field name list'
      exit
    endif else begin
      FieldNameList=selname
    endelse
    
  endif else begin
    ;NAME
  
    if ((n_elements(selfield) eq 0) OR (n_elements(sellevel) eq 0)) then begin
      numfields=n_elements(where(fieldhead(*,0)))
      fieldlist=reform(fieldhead(*,3))
      fieldlist=fieldlist(where(fieldlist))
      levellist=reform(fieldhead(*,5))
      levellist=levellist(where(levellist))
    endif else begin
      numfields=n_elements(selfield)
      fieldlist=selfield
      if (n_elements(sellevel) ne numfields) then begin
        print, 'ERROR - incorrect number of entries in level list'
        exit
      endif else begin
        levellist=sellevel
      endelse
    endelse
    
  endelse
  
  ;----------------------------------------------------
  ; Error check : Need relduation for back runs if Air Conc Percentage asked for
  
  for ifield=0,numfields-1 do begin
  
    field=fieldlist(ifield)
    
    if(back ne 0 and (relduration le 0.) and $
      (strpos(field,'Air concentration') ge 0) and (percent ne 0)) then begin
      print,'ERROR - Need to define Release Duration (relduration=??)'
      print,'in call to plotfield'
      stop
    endif else if (relduration gt 0.) then begin
      print,'WARNING - Specified relduration not used!'
    endif
  endfor
  
  ;-----------------------------------------------------------------------
  ; loop through fields and species to determine contours
  if(n_elements(contours) eq 0 or n_elements(zoom) eq 0)then begin
  
    contours=fltarr(numfields,numspecies,ncontours)
    maxconccontours=fltarr(ncontours)
    maxdepcontours=fltarr(ncontours)
    maxdosagecontours=fltarr(ncontours)
    maxdosecontours=fltarr(ncontours)
    
    
    for ifield=0,numfields-1 do begin
    
      field=fieldlist(ifield)
      level=levellist(ifield)
      
      for ispec=0,numspecies-1 do begin
      
        species=specieslist(ispec)
        ;-----------------------------------------------------------------------
        ; read first and last
        
        if (namever eq 3)then begin
          ; NAME III
        
          TimeAveraging=TimeAveragingList(ifield)
          FieldName=FieldNameList(ifield)
          
          selgrid1 = selgrid+'_C'+casenum+'_T1'
          
          readvertfield,datadir,timelist(0),selgrid1,field,level,$
            species,lon1,lat1,dat1,head1,namever=namever,timeframe=timeframe,$
            TimeAveraging=TimeAveraging,seldatavalue=seldatavalue,selname=FieldName,$
            vgrid=vgrid
          dat1=dat1*scalefactor
          
          selgrid1 = STRING(selgrid,'_C',casenum,'_T',numfiles)
          selgrid1 = STRCOMPRESS(selgrid1, /Remove_All)
          
          readvertfield,datadir,timelist(numfiles-1),selgrid1,field,level,$
            species,lon2,lat2,dat2,head2,namever=namever,timeframe=timeframe,$
            TimeAveraging=TimeAveraging,seldatavalue=seldatavalue,selname=FieldName,$
            vgrid=vgrid
          dat2=dat2*scalefactor
          
          
          ;--------------------------------------------------------------------------
          ; Fill grids to same size if necessary
          
          ;find largest z size and incerase relevant grid size
          sz1 = size(dat1)
          sz2 = size(dat2)
          
          if (sz1[2] gt sz2[2]) then begin
            lat2 = lat1
            lon2 = lon1
            zeros = fltarr(sz1[1],sz1[2]-sz2[2])
            dat2 = [[dat2],[zeros]]
          endif
          if (sz2[2] gt sz1[2]) then begin
            lat1 = lat2
            lon1 = lon2
            print, sz1[2]-sz2[2]
            zeros = fltarr(sz2[1],sz2[2]-sz1[2])
            dat1 = [[dat1],[zeros]]
          endif
          
        endif
        
        index=where(dat1 lt 1e-12*max(dat1),ntot)
        if(ntot gt 0)then dat1(index)=0.0
        index=where(dat2 lt 1e-12*max(dat2),ntot)
        if(ntot gt 0)then dat2(index)=0.0
        
        if(back ne 0 and strpos(field,'Dosage') ge 0 and $
          percent ne 0)then begin
          if(total(dat1) eq 0)then begin
            print,'No data in file'
            print,'Data array being set to zero'
            dat1=dat1*0.0
          endif else begin
            dat1=(dat1/total(dat1))*100.0
          endelse
          if(total(dat2) eq 0)then begin
            print,'No data in file'
            print,'Data array being set to zero'
            dat2=dat2*0.0
          endif else begin
            dat2=(dat2/total(dat2))*100.0
          endelse
        endif
        
        if(back ne 0 and strpos(field,'Air concentration') ge 0 and $
          percent ne 0)then begin
          if((strpos(level,'From') ge 0) or $
            (strpos(modtitle(6),'multiple') lt 0))then begin
            rate=FLOAT(strmid(modtitle(6),20,9))
            totalmass=rate*relduration
            
            dat1=dat1/totalmass*100.0
            dat2=dat2/totalmass*100.0
            
            print,modtitle(6)
            
          endif else begin
            print,'error in percent calculation - illegal options'
            stop
          endelse
        endif
        
        if(ifield eq 0 and ispec eq 0)then begin
          tot=dat1+dat2
        endif else begin
          tot=tot+dat1+dat2
        endelse
        
        ;-----------------------------------------------------------------------
        ; determine contours
        
        if(contourstep le 1.0)then begin
        
          maxdat=max([dat1,dat2])*1.1
          contours1=findgen(ncontours)/(ncontours-1)*maxdat
          
        endif else begin
        
          if(contourstep ge 3.0)then begin
            maxfac=5.0
          endif else begin
            maxfac=1.1
          endelse
          maxdat=max([dat1,dat2])*maxfac
          maxdat=max([maxdat,1.e-21])
          maxval=fix(alog10(maxdat))
          if (maxval ge 0)then maxval=maxval+1
          
          contours1=fltarr(ncontours)
          contours1(0)=10.0^maxval
          
          for ic=1,ncontours-1 do begin
            contours1(ic)=contours1(ic-1)/contourstep
          endfor
          
        endelse
        
        contours1=contours1(sort(contours1))
        contours(ifield,ispec,*)=contours1
        
        if(strpos(field,'Air concentration') ge 0)then begin
          if(max(contours1) gt max(maxconccontours))then begin
            maxconccontours=contours1
          endif
        endif
        if(strpos(field,'Dosage') ge 0)then begin
          if(max(contours1) gt max(maxdosagecontours))then begin
            maxdosagecontours=contours1
          endif
        endif
        if(strpos(field,'deposition') ge 0)then begin
          if(max(contours1) gt max(maxdepcontours))then begin
            maxdepcontours=contours1
          endif
        endif
        if(strpos(field,'Dose') ge 0)then begin
          if(max(contours1) gt max(maxdosecontours))then begin
            maxdosecontours=contours1
          endif
        endif
        
      endfor
    endfor
    
    ; fix air con and dep contours to be the same
    
    for ifield=0,numfields-1 do begin
    
      field=fieldlist(ifield)
      level=levellist(ifield)
      
      
      for ispec=0,numspecies-1 do begin
        species=specieslist(ispec)
        
        if(strpos(field,'Air concentration') ge 0)then begin
          if(rimnet gt 0)then begin
            contours(ifield,ispec,*)=[0.001,0.1,1,10,100,1000,10000,$
              100000,1e6,1e7,1e8]
          endif else begin
            contours(ifield,ispec,*)=maxconccontours
          endelse
        endif
        if(strpos(field,'Dosage') ge 0)then begin
          if(rimnet gt 0)then begin
            contours(ifield,ispec,*)=[1,100,1000,10000,100000,1e6,1e7,1e8,$
              1e9,1e10,1e11]
          endif else begin
            contours(ifield,ispec,*)=maxdosagecontours
          endelse
        endif
        if(strpos(field,'deposition') ge 0)then begin
          if(rimnet gt 0)then begin
            contours(ifield,ispec,*)=[0.1,10,100,1000,10000,100000,1e6,1e7,$
              1e8,1e9,1e10]
          endif else begin
            contours(ifield,ispec,*)=maxdepcontours
          endelse
        endif
        if(strpos(field,'Dose') ge 0)then begin
          if(rimnet gt 0)then begin
            contours(ifield,ispec,*)=[1e-8,1e-7,1e-6,1e-5,1e-4,1e-3,1e-2,1e-1,$
              1,10,100]
          endif else begin
            contours(ifield,ispec,*)=maxdosecontours
          endelse
        endif
      endfor
    endfor
    
  endif
  
  ;-----------------------------------------------------------------------
  ; plot time sequences
  
  case group of
  
    'time': begin
    
    
      ;-----------------------------------------------------------------------
      ; loop through fields and species
    
    
      for ifield=0,numfields-1 do begin
      
        field=fieldlist(ifield)
        level=levellist(ifield)
        
        if (namever eq 3)then begin
          ; NAME III
          TimeAveraging=TimeAveragingList(ifield)
          FieldName=FieldNameList(ifield)
        endif
        
        for ispec=0,numspecies-1 do begin
        
          species=specieslist(ispec)
          
          ;-----------------------------------------------------------------------
          ; open postscript file for printing
          
          if (namever eq 3)then begin
            ; NAME III
            ; First reformat level to remove '.'
            flevelsplit = strsplit(strcompress(level,/remove_all),'.',/extract)
            flevel = flevelsplit[0]+'_'+flevelsplit[1]
            filetem='Plot_'+filetext+$
              strcompress(field,/remove_all)+'_'+$
              strcompress(TimeAveraging,/remove_all)+'_'+$
              flevel+'_'+$
              selgrid+'_'+species
          endif else begin
            filetem='Plot_'+filetext+$
              strcompress(field,/remove_all)+'_'+$
              strcompress(level,/remove_all)+'_'+$
              selgrid+'_'+species
          endelse
          
          
          file=datadir+delim+filetem+'.ps'
          set_plot,'ps'
          
          device,/portrait,ysize=26.7,xsize=18,yoffset=1.5,xoffset=1.5
          device,/color
          device,filename=file
          if (ct_blue2red eq 0) then begin
            loadct,ct
            tek_color
          endif else begin
            color_blue2red,red,green,blue
            TVLCT,red,green,blue
          endelse
          
          
          ;-----------------------------------------------------------------------
          ; loop through times
          
          for it=0,numfiles-1 do begin
          
            filename=fieldfiles(it)
            
            if (namever eq 3)then begin
              ; NAME III
              filetime = timelist(it)
              
              selgrid1 = STRING(selgrid,'_C',casenum,'_T',it+1)
              selgrid1 = STRCOMPRESS(selgrid1, /Remove_All)
              
              readvertfield,datadir,filetime,selgrid1,field,level,$
                species,lon1,lat1,dat1,head1,modtitle=modtitle,$
                namever=namever,timeframe=timeframe,zrel=zrel,vgrid=vgrid,$
                timeaveraging=timeaveraging,seldatavalue=seldatavalue,selname=FieldName
                
            endif else begin
              ; NAME
              filetime = timelist(it)
              
              readvertfield,datadir,filetime,selgrid,field,level,$
                species,lon1,lat1,dat1,head1,modtitle=modtitle,$
                namever=namever,timeframe=timeframe,zrel=zrel,vgrid=vgrid
                
            endelse
            
            dat1=dat1*scalefactor
            
            index=where(dat1 lt 1e-9*max(dat1),ntot)
            if(ntot gt 0)then dat1(index)=0.0
            
            if(back ne 0)then begin
            
              ; read header of each file each time to get the correct start and end times
            
              readfieldhead,filename,modtitle,fieldhead,xyrel,$
                namever=namever,timeframe=timeframe
                
              if(back eq 1 or back eq 3)then begin
              
                cstartdt=var_to_dt(strmid(modtitle(4),36,4),strmid(modtitle(4),33,2),$
                  strmid(modtitle(4),30,2),strmid(modtitle(4),22,2),$
                  strmid(modtitle(4),24,2))
                cenddt=var_to_dt(strmid(modtitle(5),36,4),strmid(modtitle(5),33,2),$
                  strmid(modtitle(5),30,2),strmid(modtitle(5),22,2),$
                  strmid(modtitle(4),24,2))
                  
                dt_to_str,cstartdt,d1,h1,date_fmt=2,time_fmt=-2
                dt_to_str,cenddt,d2,h2,date_fmt=2,time_fmt=-2
                if (back eq 1) then attribstr=h2+'Z '+d2+' to '+h1+'Z '+d1
                if (back eq 3) then attribstr=STRMID(h2,0,2)+'Z '+d2+' - '+$
                  STRMID(h1,0,2)+'Z '+d1
                if (strpos(field,'Air concentration') ge 0) then attribstr=head1(6)
              endif else begin
                attribstr=modtitle(1)
                attribstr=strjoin(strsplit(attribstr,'UTC',/Extract),'Z',/Single)
                attribstr=strjoin(strsplit(attribstr,'_',/Extract),' ',/Single)
                attribstr=strjoin(strsplit(attribstr,'Mace Head ',/Extract),'',/Single)
              endelse
            endif
            
            if(back ne 0 and strpos(field,'Dosage') ge 0)then begin
              if(percent eq 0)then begin
                head1(3)='Back Run Time Integrated Air concentration'
                head1(4)=''
              endif else begin
                dat1=(dat1/total(dat1))*100.0
                head1(3)='Percentage contribution'
                head1(4)='%'
              endelse
            endif
            
            if(back ne 0 and strpos(field,'Air concentration') ge 0)then begin
              if(percent eq 0)then begin
                head1(3)='Back Run Air concentration'
                head1(4)=''
              endif else begin
                if(strpos(modtitle(6),'multiple') lt 0)then begin
                  rate=FLOAT(strmid(modtitle(6),20,9))
                  totalmass=rate*relduration
                  
                  dat1=dat1/totalmass*100.0
                  
                  head1(3)='Percentage contribution per m^3'
                  head1(4)='%/m^3'
                  
                endif else begin
                  print,'error in percent calculation - illegal options'
                  stop
                endelse
              endelse
            endif
            
            pos=(it) mod (nxplot*nyplot)
            multiplot=[pos,nxplot,nyplot]
            
            
            ;-----------------------------------------------------------------------
            ; plot fields
            
            contours1=reform(contours(ifield,ispec,*))
            
            if(back ne 0)then begin
              postitle=attribstr
            endif else begin
              if (namever eq 3)then begin
                ; NAME III
                postitle='Valid at '+head1(13)
              endif else begin
                ; NAME
                postitle='Valid at '+head1(6)
              endelse
            endelse
            
            if strmatch(strmid(sellevel,0,1),'Y') then hrel=xyrel[0]
            if strmatch(strmid(sellevel,0,1),'X') then hrel=xyrel[1]
            
            plotvertpos,lon1,lat1,dat1, position=position,type=type,$
              zoom=zoom,contourlevels=contours1,contourcolors=contourcolors,$
              hrel=hrel,fieldunits=head1(4),multiplot=multiplot,$
              modtitle=postitle,plotloc=plotloc,zrel=zrel,$
              plotlegend=plotlegend,$
              placenames=placenames
              
              
            ;-----------------------------------------------------------------------
            ; annotation
              
            if(plotheader ne 0 and pos eq 0)then begin
            
              xyouts,0.5,0.98,modtitle(0),$
                /normal,alignment=0.5,charsize=1.3,color=axiscol
                
              if(back eq 0)then begin
              
                xyouts,0.5,0.94,strcompress(modtitle(1))+titletext,$
                  /normal,alignment=0.5,charsize=1.3,color=axiscol
                  
              endif
              
              
              if (namever eq 3)then begin
                ; NAME III
                xyouts,0.5,0.90,head1(7)+'   '+head1(2),$
                  /normal,charsize=xysize*1.2,alignment=0.5,color=axiscol
                  
                xyouts,0.5,0.873,head1(15),$
                  /normal,charsize=xysize*1.2,alignment=0.5,color=axiscol
                  
              endif else begin
                ; NAME
              
                if (back ne 0) then begin
                  xyouts,0.5,0.90,head1(3),$
                    /normal,charsize=xysize*1.2,alignment=0.5,color=axiscol
                    
                endif else begin
                  xyouts,0.5,0.90,head1(2)+'   '+head1(3),$
                    /normal,charsize=xysize*1.2,alignment=0.5,color=axiscol
                    
                endelse
                
                ; don't write vertical extent i.e., boundary layer on pictures
                ; when plotting deposition.
                if ((strcompress(head1(3),/remove_all) ne 'Drydeposition') and $
                  (strcompress(head1(3),/remove_all) ne 'Wetdeposition') and $
                  (strcompress(head1(3),/remove_all) ne 'Totaldeposition'))then begin
                  
                  xyouts,0.5,0.873,head1(5),$
                    /normal,charsize=xysize*1.2,alignment=0.5,color=axiscol
                    
                endif
                
                
              endelse
              
              
              xyouts,0.05,0.12,modtitle(4),/normal,charsize=0.9,color=axiscol
              xyouts,0.05,0.10,modtitle(5),/normal,charsize=0.9,color=axiscol
              xyouts,0.05,0.08,modtitle(6),/normal,charsize=0.9,color=axiscol
              xyouts,0.05,0.06,modtitle(7),/normal,charsize=0.9,color=axiscol
              xyouts,0.05,0.04,modtitle(8),/normal,charsize=0.9,color=axiscol
              
              if (namever eq 3)then begin
                ; NAME III
                xyouts,0.60,0.12,'Pollutant: '+head1(3),$
                  /normal,charsize=0.9,color=axiscol
              endif else begin
                ; NAME
                xyouts,0.60,0.12,'Pollutant: '+head1(1),$
                  /normal,charsize=0.9,color=axiscol
              endelse
              
              xyouts,0.6,0.10,modtitle(3),/normal,charsize=0.9,color=axiscol
              xyouts,0.6,0.08,modtitle(2),/normal,charsize=0.9,color=axiscol
              
              if(back ne 0)then begin
                xyouts,0.6,0.06,'Model run in backward mode',$
                  /normal,charsize=0.9,color=axiscol
              endif
              
              if (printcasenum eq 1)then begin
                ; NAME III case number
                xyouts,0.60,0.04,'Case number: '+casenum,$
                  /normal,charsize=0.9,color=axiscol
              endif
              
              if (namever eq 3 and seldatavalue(0) ne ' ')then begin
                ; NAME III data value
                tokens=strsplit(seldatavalue(ifield), '=', /extract)
                dvalue=strtrim(tokens(1), 2)
                xyouts,0.60,0.02,'Data value: '+dvalue,$
                  /normal,charsize=0.9,color=axiscol
              endif
              
              if (metoffice ne 0) then begin
                xyouts,0.5,0.00,'Met Office Crown copyright',$
                  /normal,alignment=0.5,charsize=0.8,color=axiscol
                  
                image=read_image('MO_Master_B.jpg')
                loadct,0
                tv,image,0.82,0.91,true=1,xsize=0.15,/normal
                if (ct_blue2red eq 0) then begin
                  loadct,ct
                  tek_color
                endif else begin
                  TVLCT,red,green,blue
                endelse
              endif
              
            endif
            
            ;-----------------------------------------------------------------------
            if(pos eq ((nxplot*nyplot)-1))then begin
              erase
            endif
            
          endfor
          
          device,/close_file
          
          
          ;-----------------------------------------------------------------------
          ; generate gifs
          
          
          if((gengif eq 1 or genjpg eq 1 or genanim eq 1) and pc eq 0)then begin
          
            genanim,file,datadir,filetem,gengif=gengif,genjpg=genjpg,$
              genanim=genanim,xpixel=xpixel
              
          endif
          
        endfor
      endfor
      
    end
    
    
    'field':  begin
    
      numpages=((numfields-1)/(nxplot*nyplot))+1
      
      ;-----------------------------------------------------------------------
      ; loop through fields and species
      
      for ispec=0,numspecies-1 do begin
      
        species=specieslist(ispec)
        
        
        ; determine number of pages (if number of fields > number of plots per page)
        
        for ipage=0,numpages-1 do begin
        
          filenum=string(ipage,format='(i2.2)')
          startfield=nxplot*nyplot*ipage
          endfield=(nxplot*nyplot*(ipage+1))
          endfield=min([endfield,numfields])
          
          
          ;-----------------------------------------------------------------------
          ; open postscript file for printing
          
          filetem='Plot_'+filetext+'fields'+filenum+'_'+selgrid+'_'+species
          file=datadir+delim+filetem+'.ps'
          set_plot,'ps'
          
          device,/portrait,ysize=26.7,xsize=18,yoffset=1.5,xoffset=1.5
          device,/color
          device,filename=file
          if (ct_blue2red eq 0) then begin
            loadct,ct
            tek_color
          endif else begin
            color_blue2red,red,green,blue
            TVLCT,red,green,blue
          endelse
          
          ;-----------------------------------------------------------------------
          ; loop through times
          
          for it=0,numfiles-1 do begin
          
            filename=fieldfiles(it)
            filetime = timelist(it)
            
            if(back ne 0)then begin
              if(back eq 1 or back eq 3)then begin
                dt_to_str,cstartdt,d1,h1,date_fmt=2,time_fmt=-2
                dt_to_str,cenddt,d2,h2,date_fmt=2,time_fmt=-2
                attribstr=STRMID(h2,0,2)+'Z '+d2+' to '+STRMID(h1,0,2)+'Z '+d1
              endif else begin
                attribstr=modtitle(1)
                attribstr=strjoin(strsplit(attribstr,'UTC',/Extract),'Z',/Single)
                attribstr=strjoin(strsplit(attribstr,'_',/Extract),' ',/Single)
                attribstr=strjoin(strsplit(attribstr,'Mace Head ',/Extract),'',/Single)
              endelse
            endif
            
            ;-----------------------------------------------------------------------
            ; loop through fields and species
            
            for ifield=startfield,endfield-1 do begin
            
              if (namever eq 3)then begin
                ; NAME III
                field=fieldlist(ifield)
                level=levellist(ifield)
                TimeAveraging=TimeAveragingList(ifield)
                FieldName=FieldNameList(ifield)
                
                selgrid1 = STRING(selgrid,'_C',casenum,'_T',it+1)
                selgrid1 = STRCOMPRESS(selgrid1, /Remove_All)
                
                readvertfield,datadir,filetime,selgrid1,field,level,$
                  species,lon1,lat1,dat1,head1,namever=namever,timeframe=timeframe,$
                  timeaveraging=timeaveraging,seldatavalue=seldatavalue,selname=FieldName,$
                  zrel=zrel
                  
              endif else begin
                ; NAME
                field=fieldlist(ifield)
                level=levellist(ifield)
                
                readvertfield,datadir,filetime,selgrid,field,level,zrel=zrel,$
                  species,lon1,lat1,dat1,head1,namever=namever,timeframe=timeframe
                  
              endelse
              
              dat1=dat1*scalefactor
              
              pos=(ifield) mod (nxplot*nyplot)
              multiplot=[pos,nxplot,nyplot]
              
              if(back ne 0 and strpos(field,'Dosage') ge 0)then begin
                if(percent eq 0)then begin
                  head1(3)='Back Run Time Integrated Air concentration'
                  head1(4)=''
                endif else begin
                  dat1=(dat1/total(dat1))*100.0
                  head1(3)='Percentage contribution'
                  head1(4)='%'
                endelse
                lenlevel=STRLEN(level)
                attribstr1=attribstr+STRMID(level,4,lenlevel-4)
              endif
              
              
              if(back ne 0 and strpos(field,'Air concentration') ge 0)then begin
                if(percent eq 0)then begin
                  head1(3)='Back Run Air concentration'
                  head1(4)=''
                endif else begin
                  if(strpos(modtitle(6),'multiple') lt 0)then begin
                    rate=FLOAT(strmid(modtitle(6),20,9))
                    totalmass=rate*relduration
                    
                    dat1=dat1/totalmass*100.0
                    
                    head1(3)='Percentage contribution per m^3'
                    head1(4)='%/m^3'
                    
                  endif else begin
                    print,'error in percent calculation - illegal options'
                    stop
                  endelse
                endelse
              endif
              
              
              
              ;-----------------------------------------------------------------------
              ; plot fields
              
              contours1=reform(contours(ifield,ispec,*))
              
              if (namever eq 3)then begin
                ; NAME III
                if(back ne 0)then begin
                  postitle=attribstr1
                endif else begin
                  postitle=head1(15)+' '+head1(2)
                endelse
              endif else begin
                ; NAME
                if(back ne 0)then begin
                  postitle=attribstr1
                endif else begin
                  postitle=head1(5)+' '+head1(3)
                endelse
              endelse
              
              if strmatch(strmid(sellevel,0,1),'Y') then hrel=xyrel[0]
              if strmatch(strmid(sellevel,0,1),'X') then hrel=xyrel[1]
              
              plotvertpos,lon1,lat1,dat1, position=position,type=type,$
                zoom=zoom,contourlevels=contours1,contourcolors=contourcolors,$
                hrel=hrel,fieldunits=head1(4),multiplot=multiplot,$
                modtitle=postitle,plotloc=plotloc,zrel=zrel,$
                plotlegend=plotlegend,$
                placenames=placenames
                
              ;-----------------------------------------------------------------------
              ; annotation
                
                
              if(plotheader ne 0 and pos eq 0)then begin
              
                xyouts,0.5,0.98,modtitle(0),$
                  /normal,alignment=0.5,charsize=1.3,color=axiscol
                  
                xyouts,0.5,0.94,strcompress(modtitle(1)),$
                  /normal,alignment=0.5,charsize=1.3,color=axiscol
                  
                if (namever eq 3)then begin
                  ; NAME III
                  if (back ne 0) then begin
                    xyouts,0.5,0.90,'Attribution back to '+head1(13),$
                      /normal,charsize=xysize*1.2,alignment=0.5,color=axiscol
                  endif else begin
                    xyouts,0.5,0.90,'Valid at '+head1(13),$
                      /normal,charsize=xysize*1.2,alignment=0.5,color=axiscol
                  endelse
                endif else begin
                  ; NAME
                  if (back ne 0) then begin
                    xyouts,0.5,0.90,'Attribution back to '+head1(6),$
                      /normal,charsize=xysize*1.2,alignment=0.5,color=axiscol
                  endif else begin
                    xyouts,0.5,0.90,'Valid at '+head1(6),$
                      /normal,charsize=xysize*1.2,alignment=0.5,color=axiscol
                  endelse
                endelse
                
                xyouts,0.05,0.12,modtitle(4),/normal,charsize=0.9,color=axiscol
                xyouts,0.05,0.10,modtitle(5),/normal,charsize=0.9,color=axiscol
                xyouts,0.05,0.08,modtitle(6),/normal,charsize=0.9,color=axiscol
                xyouts,0.05,0.06,modtitle(7),/normal,charsize=0.9,color=axiscol
                xyouts,0.05,0.04,modtitle(8),/normal,charsize=0.9,color=axiscol
                if (namever eq 3)then begin
                  ; NAME III
                  xyouts,0.60,0.12,'Pollutant: '+head1(3),$
                    /normal,charsize=0.9,color=axiscol
                endif else begin
                  ; NAME
                  xyouts,0.60,0.12,'Pollutant: '+head1(1),$
                    /normal,charsize=0.9,color=axiscol
                endelse
                xyouts,0.6,0.10,modtitle(3),/normal,charsize=0.9,color=axiscol
                xyouts,0.6,0.08,modtitle(2),/normal,charsize=0.9,color=axiscol
                if (printcasenum eq 1)then begin
                  ; NAME III case number
                  xyouts,0.60,0.04,'Case number: '+casenum,$
                    /normal,charsize=0.9,color=axiscol
                endif
                if (namever eq 3 and seldatavalue(0) ne ' ')then begin
                  ; NAME III data value
                  tokens=strsplit(seldatavalue(ifield), '=', /extract)
                  dvalue=strtrim(tokens(1), 2)
                  xyouts,0.60,0.02,'Data value: '+dvalue,$
                    /normal,charsize=0.9,color=axiscol
                endif
                
                if (metoffice ne 0) then begin
                  xyouts,0.5,0.00,'Met Office Crown copyright',$
                    /normal,alignment=0.5,charsize=0.8,color=axiscol
                    
                  image=read_image('MO_Master_B.jpg')
                  loadct,0
                  tv,image,0.82,0.91,true=1,xsize=0.15,/normal
                  if (ct_blue2red eq 0) then begin
                    loadct,ct
                    tek_color
                  endif else begin
                    TVLCT,red,green,blue
                  endelse
                endif
                
              endif
              
              
              ;-----------------------------------------------------------------------
              ; new page
              
              if(pos eq ((nxplot*nyplot)-1) or ifield eq endfield-1)then begin
                erase
              endif
              
            endfor
          endfor
          
          device,/close_file
          
          
          ;-----------------------------------------------------------------------
          ; generate gifs
          
          if((gengif eq 1 or genjpg eq 1 or genanim eq 1) and pc eq 0)then begin
            genanim,file,datadir,filetem,gengif=gengif,genjpg=genjpg,$
              genanim=genanim,xpixel=xpixel
          endif
          
        endfor
      endfor
      
    end
    
  endcase
  
END
